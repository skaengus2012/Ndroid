buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'

        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}


group = project.property('release_group')
version = project.property('release_version')

allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'com.android.library'
    apply plugin: 'com.github.dcendents.android-maven'
    apply plugin: 'me.tatarka.retrolambda'
    apply plugin: 'com.jfrog.bintray'
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName project.property('release_version')

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile 'com.android.support:support-v4:25.1.0'
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:support-annotations:25.1.0'

    testCompile 'junit:junit:4.12'
}

def gitUrl = 'https://github.com/skaengus2012/Ndroid'
def gitDevelopUrl = 'https://github.com/skaengus2012/Ndroid.git'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Ndroid'
            description 'Android MVP & Functional library'
            url gitUrl
            inceptionYear '2017'

            packaging 'aar'
            groupId project.property('release_group')
            artifactId 'Ndroid-appfactory'
            version project.property('release_version')

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitDevelopUrl
                url gitUrl
            }
            developers {
                developer {
                    name 'Doohyun Nam'
                }
            }
        }
    }
}

bintray {
    user = project.property('bintrayUser')
    key = project.property('bintrayApiKey')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = user
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
        vcsUrl = gitUrl
        version {
            name = project.property('release_version')
            desc = 'Android MVP & Functional library'
            vcsTag = project.property('release_version')
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}